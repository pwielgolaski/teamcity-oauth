buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.4.1'
        classpath 'com.github.rodm:gradle-teamcity-plugin:1.0'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.jfrog.bintray'

scmVersion {
    tag {
        prefix = project.name
    }
    versionCreator 'versionWithBranch'
    localOnly = true
}

version = scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    teamcityVersion = '2017.1'
    teamcityDir = "$rootDir/servers/TeamCity-${teamcityVersion}"
    teamcityDataDir = "$rootDir/data/"
    teamcityJavaHome = System.properties['java.home']
    okhttpVersion = '3.8.1'
}

dependencies {
    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    compile('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: 'junit'
    }
    // mandatory dependencies for using Spock
    testCompile 'org.codehaus.groovy:groovy-all:2.4.1'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
}

teamcity {
    version = teamcityVersion
    server {
        descriptor {
            name = project.name
            displayName = project.name
            version = project.version
            vendorName = 'wielgolaski.me'
            description = 'oAuth2 authentication plugin'
            downloadUrl = 'https://github.com/pwielgolaski/teamcity-oauth/'
            email = 'pwielgolaski@gmail.com'
        }
    }
    environments {
        teamcity {
            version = teamcityVersion
            homeDir = file(teamcityDir)
            dataDir = file(teamcityDataDir)
            javaHome = file(teamcityJavaHome)
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    filesSpec {
        from 'build/distributions'
        into '.'
    }
    pkg {
        repo = 'generic'
        name = project.name
        desc = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/pwielgolaski/teamcity-oauth.git'
        labels = ['teamcity', 'oauth']
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'TeamCity oAuth authentication Plugin'
        }
    }
}

bintrayUpload.onlyIf {
    !(version ==~ /.*SNAPSHOT/)
}